
namespace GBRMaker;

public struct MainSettingData
{
    int center_x;
    int center_y;
    int image_w;
    int image_h;

    int unit_length;

    string project_name;
    string data_path;
}

public struct UIMainSettingChangedEvent : ALittle.UIEvent
{
    MainSettingData data;
}

public class MainSettingDialog : ALittle.Dialog
{
	private ALittle.ImageInput _center_x_input; // 单位图片中心X坐标
	private ALittle.ImageInput _center_y_input; // 单位图片中心Y坐标
	private ALittle.ImageInput _image_h_input; // 单位图片高
	private ALittle.ImageInput _image_w_input; // 单位图片宽
	private ALittle.ImageInput _unit_length_input; // 单位格子边长
	private ALittle.ImageInput _project_name_input; // 项目名
	private ALittle.ImageInput _data_path_input; // 场景数据路径

    private MainSettingData _data;

    [Constant]
    public get data() : const MainSettingData { return this._data; }

    private fun TCtor()
    {
        // 获取配置
        this._data = g_GConfig.GetConfig("setting_data", new MainSettingData());

        // 数据初始化
        if (this._data.project_name == null) this._data.project_name = "GBR";
        if (this._data.data_path == null) this._data.data_path = "Scene";

        if (this._data.center_x == null) this._data.center_x = 0;
        if (this._data.center_y == null) this._data.center_y = 0;
        if (this._data.image_w == null) this._data.image_w = 100;
        if (this._data.image_h == null) this._data.image_h = 100;
        if (this._data.unit_length == null) this._data.unit_length = 100;

        // 初始化界面
        this._project_name_input.text = this._data.project_name;
        this._data_path_input.text = this._data.data_path;

        this._center_x_input.text = this._data.center_x;
        this._center_y_input.text = this._data.center_y;
        this._image_w_input.text = this._data.image_w;
        this._image_h_input.text = this._data.image_h;
        this._unit_length_input.text = this._data.unit_length;
    }

    private fun HandleSettingCancelClick(ALittle.UIClickEvent event) { this.visible = false; }
    private fun HandleSettingConfirmClick(ALittle.UIClickEvent event)
    {
        var center_x = ALittle.Math_ToInt(this._center_x_input.text);
        var center_y = ALittle.Math_ToInt(this._center_y_input.text);
        var image_w = ALittle.Math_ToInt(this._image_w_input.text);
        var image_h = ALittle.Math_ToInt(this._image_h_input.text);
        var unit_length = ALittle.Math_ToInt(this._unit_length_input.text);

        if (center_x == null || center_x < 0
            || center_y == null || center_y < 0
            || image_w == null || image_w < 0
            || image_h == null || image_h < 0
        	|| unit_length == null || unit_length < 0)
        { g_AUITool.ShowNotice("提示", "请输入正整数"); return; }

        var module_path = "Module/"..this._project_name_input.text;
        if (ALittle.File_GetFileAttr(module_path) == null)
        { g_AUITool.ShowNotice("提示", "项目不存在"); return; }

        if (ALittle.File_GetFileAttr(module_path.."/Other/"..this._data_path_input.text) == null)
        { g_AUITool.ShowNotice("提示", "场景数据路径不存在"); return; }

        this._data.center_x = center_x;
        this._data.center_y = center_y;
        this._data.image_w = image_w;
        this._data.image_h = image_h;
        this._data.unit_length = unit_length;

        this._data.project_name = this._project_name_input.text;
        this._data.data_path = this._data_path_input.text;

        g_GConfig.SetConfig("setting_data", this._data);

        this.visible = false;

        var changed_event = new UIMainSettingChangedEvent();
        changed_event.data = this._data;
        this.DispatchEvent(event);
    }
}

namespace BattleCity;

public class LoginScene : ALittle.DisplayLayout
{
	private ALittle.Text _last_score;
	private ALittle.Text _high_score;
	private ALittle.Text _1_player_text;
	private ALittle.Text _2_player_text;
	private ALittle.Text _construction_text;
    private ALittle.Text _edit_text;
	private ALittle.FramePlay _cursor;

    private int _select_option = 1;

    private ALittle.LoopList _anti_loop;

    private fun TCtor()
    {
        this._high_score.text = g_GConfig.GetInt("high_score", 2000);
        this._last_score.text = g_GConfig.GetInt("last_score", 0);

        this._cursor.Stop();
        this.disabled = true;

        this._edit_text.visible = A_ModuleSystem.GetDebugInfo() == "debug";
    }

    public fun Show()
    {
        this.visible = true;
        if (this._anti_loop != null)
        {
            this._anti_loop.Stop();
            this._anti_loop = null;
        }

        this._y = A_UISystem.view_height;
        this._cursor.visible = false;

        this._anti_loop = new ALittle.LoopList();
        this._anti_loop.AddUpdater(new ALittle.LoopLinear(this, "y", 0, 10000, 0));
        this._anti_loop.AddUpdater(new ALittle.LoopAttribute(this._cursor, "visible", true, 0));
        this._anti_loop.AddUpdater(new ALittle.LoopTimer(bind(this._cursor.Play, this._cursor), 0));

        this._anti_loop.Start();

        A_UISystem.keydown_callback = bind(this.HandleKeyDown, this);
    }

    public fun Hide()
    {
        if (this._anti_loop != null)
        {
            this._anti_loop.Stop();
            this._anti_loop = null;
        }

        this._cursor.Stop();
        this.visible = false;
    }

    private fun HandleKeyDown(int mod, int sym, int scancode)
    {
        if (this._cursor.visible == false)
        {
            if (sym == ALittle.UIEnumTypes.KEY_G || sym == ALittle.UIEnumTypes.KEY_H)
                this._anti_loop.SetCompleted();
            return;
        }

        if (sym == ALittle.UIEnumTypes.KEY_G)
        {
            this._select_option += 1;

            if (A_ModuleSystem.GetDebugInfo() == "debug")
            {
                if (this._select_option > 4)
                	this._select_option = 1;
            }
            else
            {
                if (this._select_option > 3)
                	this._select_option = 1;
            }

            if (this._select_option == 1)
            	this._cursor.y = this._1_player_text.y + this._1_player_text.height / 2 - this._cursor.height / 2;
            elseif (this._select_option == 2)
            	this._cursor.y = this._2_player_text.y + this._2_player_text.height / 2 - this._cursor.height / 2;
            elseif (this._select_option == 3)
            	this._cursor.y = this._construction_text.y + this._construction_text.height / 2 - this._cursor.height / 2;
            elseif (this._select_option == 4)
            	this._cursor.y = this._edit_text.y + this._edit_text.height / 2 - this._cursor.height / 2;
            
            return;
        }

        if (sym == ALittle.UIEnumTypes.KEY_H)
        {
            this.Hide();

            if (this._select_option == 3)
            	g_GCenter.StartConstruction();
            elseif (this._select_option == 4)
                g_GCenter.StartEdit();
            else
            	g_GCenter.StartPlay(this._select_option);
        }
    }
}

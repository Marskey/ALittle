
namespace ALittle;

struct CSVPreloadInfoDetail
{
    lua.__CPPAPICsvFileLoader loader;
    CsvConfig config;
}

struct CSVPreloadInfo
{
    int total;
    int succeed;
    int failed;
    Map<int, CSVPreloadInfoDetail> loader_map;
    Functor<(int, int, int)> callback;
}

class CSVConfigManager
{
    private Map<CSVPreloadInfo, bool> _preload_map;

    public ctor()
    {
        // csv预加载映射表
        this._preload_map = new Map<CSVPreloadInfo, bool>();
    }

    public fun PrepareCsv(Map<string, CsvConfig> csv_map, Functor<(int, int, int)> callback)
    {
        var preload_info = new CSVPreloadInfo();
        preload_info.total = 0;
        preload_info.succeed = 0;
        preload_info.failed = 0;
        preload_info.loader_map = new Map<int, CSVPreloadInfoDetail>();
        preload_info.callback = callback;

        for (var file_path, config in csv_map)
        {
            var loader = new lua.__CPPAPICSVFileLoader();
            loader.SetPath(file_path, false, String_Join(config.GetFieldNameList(), ","));
            var detail = new CSVPreloadInfoDetail();
            detail.loader = loader;
            detail.config = config;
            preload_info.loader_map[loader.GetID()] = detail;
            loader.Start();

            ++ preload_info.total;
        }
        if (preload_info.total > preload_info.succeed + preload_info.failed)
            this._preload_map[preload_info] = true;
        
        // 调用回调通知
        if (callback != null)
            callback(preload_info.total, preload_info.succeed, preload_info.failed);
    }

    public fun HandleCSVFileLoadSucceed(lua.__CPPAPICSVFileLoader loader, lua.__CPPAPICSVFile file)
    {
        var id = loader.GetID();
        for (var info, value in this._preload_map)
        {
            var detail = info.loader_map[id];
            if (detail != null)
            {
                ++info.succeed;

                if (detail.config != null)
                {
                    var wrap = new lua.__CPPAPICSVFileWrap();
                    wrap.SetCSVFile(file);
                    detail.config.Init(wrap);
                }

                if (info.callback != null)
                    info.callback(info.total, info.succeed, info.failed);

                if (info.total == info.succeed + info.failed)
                    this._preload_map[info] = null;

                return;
            }
        }
    }

    public fun HandleCSVFileLoadFailed(lua.__CPPAPICSVFileLoader loader)
    {
        Error("csv load failed! path:"..loader.GetPath());
        var id = loader.GetID();
        for (var info, value in this._preload_map)
        {
            var detail = info.loader_map[id];
            if (detail != null)
            {
                ++info.failed;

                if (detail.config != null)
                    detail.config.Init(null);

                if (info.callback != null)
                    info.callback(info.total, info.succeed, info.failed);

                if (info.total == info.succeed + info.failed)
                    this._preload_map[info] = null;

                return;
            }
        }
    }
}

public var A_CSVConfigManager = new CSVConfigManager();
namespace ALittle;

public class IConfigSystem
{
    public fun GetConfig<T>(string key, T default) : T { return default; }   
    public fun GetBool(string key, bool default) : bool { return default; }
    public fun GetInt(string key, int default) : int { return default; }
    public fun GetDouble(string key, double default) : double { return default; }
    public fun GetString(string key, string default) : string { return default; }
    public fun SetConfig(string key, any value, [Nullable] bool not_save) {}
    public fun CoverConfig(Map<string, any> msg, bool save) {}
    public fun SaveConfig() {}
}

public class ConfigSystem<FL : IFileLoader, FS : IFileSaver> : IConfigSystem
{
    private string              _file_path;
    private Map<string, any>    _config_map;

    public ctor(string file_path, [Nullable] bool print_error)
    {
        // 保存路径
        this._file_path = file_path;
        // 定义KEY-VALUE映射
        this._config_map = new Map<string, any>();
        
        // 读取全部内容
        var file = new FL();
        var content = file.Load(this._file_path);
        if (content == null)
        {
            if (print_error)
                Error("config load failed." .. file_path);
            return;
        }
        
        // 解析配置文件
        var error, json_content = tcall(String_JsonDecode, content);
        if (error != null)
        {
            Log("Json Decode failed." .. file_path .. ", " .. error);
            return;
        }
        // 保存解析内容
        this._config_map = json_content;
    }
    
    public fun GetConfig<T>(string key, T default) : T
    {
        // 获取值
        var value = cast<T>(this._config_map[key]);
        // 如果为空则返回默认值
        if (value == null) return default;
        return value;
    }
    
    public fun GetBool(string key, bool default) : bool
    {
        var value = this.GetConfig(key, default);
        return value != null && value != false;
    }
    
    public fun GetInt(string key, int default) : int
    {
        var value = this.GetConfig(key, default);
        if (value == null) value = 0;
        return value;
    }
    
    public fun GetDouble(string key, double default) : double
    {
        var value = this.GetConfig(key, default);
        if (value == null) value = 0;
        return value;
    }
    
    public fun GetString(string key, string default) : string
    {
        var value = this.GetConfig(key, default);
        if (value == null) value = "";
        return value;
    }
    
    public fun SetConfig(string key, any value, [Nullable] bool not_save)
    {
        // 保存新值
        this._config_map[key] = value;
        if (!not_save)
            this.SaveConfig();
    }
    
    public fun CoverConfig(Map<string, any> msg, bool save)
    {
        for (var k, v in msg)
            this._config_map[k] = v;
        if (!save)
            this.SaveConfig();
    }
    
    public fun SaveConfig()
    {
        // 保存配置值
        var file = new FS();
        if (!file.Save(this._file_path, String_JsonEncode(this._config_map)))
            Log("Save Congig Failed.", this._file_path);
    }
}

public using NormalConfigSystem = ConfigSystem<NormalFileLoader, NormalFileSaver>;
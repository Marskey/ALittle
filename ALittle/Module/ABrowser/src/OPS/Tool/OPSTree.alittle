
namespace ABrowser;

protected class OPSTree : ALittle.DisplayLayout
{
	private ALittle.DisplayObject _head;
	private ALittle.DisplayGroup _body;

    public fun TCtor()
    {
    	this._head.selected = false;
    	this.height = this._body.y;
    	this._body.visible = false;
    }
    
    public set text(string value) { this._head.text = value; }
    public get text() : string { return this._head.text; }
    public get childs() : List<ALittle.DisplayObject> { return this._body.childs; }
    public get body() : ALittle.DisplayGroup { return this._body; }
    
    public fun AddItem(ALittle.DisplayObject item)
    {
    	this._body.AddChild(item);
    	item._logic_parent = this;
    }
    
    public fun RemoveChild(ALittle.DisplayObject item) : bool
    {
    	return this._body.RemoveChild(item);
    }
    
    public fun Show()
    {
    	this._body.visible = true;
    	this._head.selected = true;
    
    	this.height = this._body.y + this._body.height;
    	this.DispatchEvent(new ALittle.UIResizeEvent());
    }
    
    public fun Hide()
    {
    	this._body.visible = false;
    	this._head.selected = false;
    
    	this.height = this._body.y;
    	this.DispatchEvent(new ALittle.UIResizeEvent());
    }
    
    // 获取总高度
    public get height() : double
    {
    	if (this._body.visible == false) return this._body.y;
    	return this._body.y + this._body.height;
    }
    
    public fun HandleHeadChanged(ALittle.UIEvent event)
    {
    	if (this._head.selected)
    		this.Show();
    	else
    		this.Hide();
    }
}
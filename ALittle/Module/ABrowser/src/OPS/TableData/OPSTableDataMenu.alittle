
namespace ABrowser;

protected enum OPSTableDataMenuType
{
    SHOW_RIGHT = "OPS_OPSTableDataMenu_SHOW_RIGHT";
    SHOW_BOTTOM = "OPS_OPSTableDataMenu_SHOW_BOTTOM";
}

protected struct OPSTableDataMenuButtonUserData
{
	OPSHandleConfig dialog_config;
	OPSTableData table_data;
	bool batch;					// 是否批量处理
	Map<string, any> data;
}

protected class OPSTableDataMenu
{
    private OPSCenter _center;
    
    private ALittle.Linear _right_linear;

    public ctor(OPSCenter center)
    {
    	this._center = center;
    }

    public fun Show(ALittle.DisplayObject target)
    {
    	// 右键菜单最外层容器
    	if (this._right_linear == null)
    	{
    		this._right_linear = new ALittle.Linear(g_Control);
    		this._right_linear.type = ALittle.UIEnumTypes.TYPE_V;
    		this._right_linear.width = 100;
    	}
    	this._right_linear.RemoveAllChild();

		var user_data:OPSTableDataBatchButtonUserData = target._user_data;
    	// 创建按钮
    	var handle_config_list = user_data.single_handle_config_list;
    	if (handle_config_list == null) handle_config_list = user_data.multy_handle_config_list;
    	if (handle_config_list == null) return;
    	for (var index, handle_config in handle_config_list)
    	{
    		var button = g_Control.CreateControl{ALittle.DisplayObject}("ops_common_button");
    		button.AddEventListener(this, this.HandleChildClick);
    		button.text = handle_config.name;
    		this._right_linear.AddChild(button);

			var btn_user_data = new OPSTableDataMenuButtonUserData();
    		button._user_data = btn_user_data;
    		btn_user_data.dialog_config = handle_config; 		// 配置信息
    		btn_user_data.data = user_data.data; 				// 数据
    		btn_user_data.table_data = user_data.table_data; 	// 所在的tab
    		btn_user_data.batch = user_data.batch; 				// 是否是批量
    	}

    	// 调整位置
    	var x, y = target.LocalToGlobal();
    	if (x < 0) x = 0;
    	elseif (x > A_UISystem.view_width) x = A_UISystem.view_width - target.width;

    	if (y < 0) y = 0;
    	elseif (y + this._right_linear.height > A_UISystem.view_height) y = A_UISystem.view_height - this._right_linear.height;

    	if (user_data.position == OPSTableDataMenuType.SHOW_BOTTOM)
    	{
    		this._right_linear.x = x;
    		this._right_linear.y = y + target.height;
    	}
    	elseif (user_data.position == OPSTableDataMenuType.SHOW_RIGHT)
    	{
    		this._right_linear.x = x + target.width;
    		this._right_linear.y = y;
    	}

    	// 显示在右键菜单
    	A_LayerManager.ShowFromRight(this._right_linear);
    }

    public fun HandleChildClick(ALittle.UIClickEvent event)
    {
    	A_LayerManager.HideFromRight(this._right_linear);
    	this._center.dialog_logic.Show(event.target);
    }
}
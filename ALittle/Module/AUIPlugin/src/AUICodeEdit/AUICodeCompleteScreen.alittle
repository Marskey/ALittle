
namespace AUIPlugin;

protected struct AUICodeCompleteItemInfo
{
    ALittle.TextRadioButton _item_button;
    ALittle.Text _item_title;
    ALittle.Image _tag_image;
    ALittle.DisplayObject _item;

    int pos;        // 用于排序
    string upper;
    lua.ABnfQueryCompleteInfo complete;
}

protected class AUICodeCompleteScreen
{
    private ALittle.ScrollScreen _screen;
    private List<AUICodeCompleteItemInfo> _item_list;

    private List<AUICodeCompleteItemInfo> _item_pool = new List<AUICodeCompleteItemInfo>();
    private int _item_pool_count = 0;

    private ALittle.DisplayObject _tip_dialog;
    private ALittle.TextArea _tip_text;
    
    private ALittle.TextRadioButtonGroup _item_group;

    private lua.ABnfQueryComplete _complete;
    private int _line_end;
    private int _char_end;
    private AUICodeEdit _edit;
    private double _item_height = 0;

    public get edit() : AUICodeEdit { return this._edit; }

    public fun Shutdown()
    {
        this.Hide();
    }

    public async fun ShowComplete(AUICodeEdit edit)
    {
        this._edit = edit;
        if (this._complete == null)
        {
            if (!this.ReInit(edit))
            {
                this.Hide();
                return;
            }
        }

        this._line_end = edit.cursor.line;
        this._char_end = edit.cursor.char;
        var text = edit.GetTargetText(this._complete.line_start, this._complete.char_start - 1, edit.cursor.line, edit.cursor.char);
        if (text == null || text == "" || !this.Fliter(text))
        {
            this.Hide();
            return;
        }
    }

    public fun IsShow() : bool { return this._complete != null; }

    public fun SelectUp()
    {
        var target = this.GetSelectIndex();
        target -= 1;
        if (target < 1) return;
        var item = this._screen.childs[target];
        var info = cast<AUICodeCompleteItemInfo>(item._user_data);
        info._item_button.selected = true;

        if (info.complete.descriptor != null)
            this.ShowTip(info.complete.descriptor);
        else
            this.HideTip();

        var delta = this._screen.container.height - this._screen.height;
        if (delta > 0)
        {
            var offset = (target - 1) * this._item_height + this._screen.container_y;
            if (offset < 0)
            {
                this._screen.right_scrollbar.offset_rate = ((target - 1) * this._item_height) / delta; 
                this._screen.RejustScrollBar();
            }
        }
    }

    public fun SelectDown()
    {
        var target = this.GetSelectIndex();
        target += 1;
        if (target > this._screen.child_count) return;
        var item = this._screen.childs[target];
        var info = cast<AUICodeCompleteItemInfo>(item._user_data);
        info._item_button.selected = true;

        if (info.complete.descriptor != null)
            this.ShowTip(info.complete.descriptor);
        else
            this.HideTip();
        
        var delta = this._screen.container.height - this._screen.height;
        if (delta > 0)
        {
            var offset = target * this._item_height + this._screen.container_y;
            if (offset > this._screen.height)
            {
                this._screen.right_scrollbar.offset_rate = (target * this._item_height - this._screen.height) / delta;   
                this._screen.RejustScrollBar();   
            }
        }
    }

    public fun DoSelect() : bool
    {
        var target = this.GetSelectIndex();
        if (target == null) return false;

        this._edit.select_cursor.StartLineChar(this._complete.line_start, this._complete.char_start - 1);
        this._edit.select_cursor.UpdateLineChar(this._edit.cursor.line, this._edit.cursor.char);

        var item = this._screen.childs[target];
        var text:string;
        var complete = cast<AUICodeCompleteItemInfo>(item._user_data).complete;
        if (complete.insert != null) text = complete.insert;
        else text = complete.display;

        var result = this._edit.InsertText(text, true);
        this.Hide();
        return result;
    }

    private fun GetSelectIndex() : int
    {
        var target:int = null;
        for (var index, child in this._screen.childs)
        {
            if (cast<AUICodeCompleteItemInfo>(child._user_data)._item_button.selected)
            {
                target = index;
                break;
            }   
        }
        return target;
    }

    private await fun ReInit(AUICodeEdit edit) : bool
    {
        if (edit.language == null) return false;
        this._complete = edit.language.QueryComplete(edit.cursor.line, edit.cursor.char - 1);
        if (this._complete == null) return false;
        
        // 计算坐标
        var x, y = edit.CalcAbsPosition(this._complete.line_start, this._complete.char_start, true);
        y += edit.line_height;

        if (this._screen == null)
        {
            this._screen = g_Control.CreateControl{ALittle.ScrollScreen}("ide_code_scroll_screen");
            this._screen.width = 200;
            this._screen.AddEventListener(this, this.HandleHideEvent);
        }

        this._screen.x = x;
        this._screen.y = y;

        if (this._item_list != null)
        {
            for (var index, info in this._item_list)
            {
                this._item_pool_count += 1;
                this._item_pool[this._item_pool_count] = info;
            }
        }

        this._item_group = new ALittle.TextRadioButtonGroup();
        this._item_list = new List<AUICodeCompleteItemInfo>();
        for (var index, info in this._complete.complete_list)
        {
            var item_info:AUICodeCompleteItemInfo;
            if (this._item_pool_count > 0)
            {
                item_info = this._item_pool[this._item_pool_count];
                this._item_pool[this._item_pool_count] = null;
                this._item_pool_count -= 1;
            }
            else
            {
                item_info = new AUICodeCompleteItemInfo();   
                item_info._item = g_Control.CreateControl{ALittle.DisplayObject}("ide_code_complete_item", item_info);
            }
            item_info._item_button.group = this._item_group;
            item_info._item_title.text = info.display;
            item_info.upper = ALittle.String_Upper(info.display);
            item_info._tag_image.texture_name = edit.language.QueryCompleteIcon(info.tag);
            item_info._item._user_data = item_info;
            item_info.complete = info;
            this._item_list[index] = item_info;

            this._item_height = item_info._item.height;
        }

        A_LayerManager.ShowFromRight(this._screen, false);

        return true;
    }

    private static ItemInfoSort(AUICodeCompleteItemInfo a, AUICodeCompleteItemInfo b) : bool
    {
        return a.pos < b.pos;
    }

    private fun Fliter(string text) : bool
    {
        var descriptor:string;
        var upper = ALittle.String_Upper(text);

        var sort_list = new List<AUICodeCompleteItemInfo>();
        var count = 0;
        this._screen.RemoveAllChild();
        for (var index, info in this._item_list)
        {
            info.pos = ALittle.String_Find(info.upper, upper);
            if (info.pos != null)
            {
                ++count;
                sort_list[count] = info;
            }
        }
        ALittle.List_Sort(sort_list, AUICodeCompleteScreen.ItemInfoSort);
        for (var index, info in sort_list)
        {
            if (this._screen.child_count == 0)
            {
                info._item_button.selected = true;
                descriptor = info.complete.descriptor;
            }
            this._screen.AddChild(info._item);      
        }

        if (this._screen.child_count == 0) return false;

        var height = this._screen.child_count * this._item_height;
        if (height < 200) this._screen.height = height;
        else this._screen.height = 200;

        if (descriptor != null)
            this.ShowTip(descriptor);
        else
            this.HideTip();

        return true;
    }

    public fun Hide()
    {
        this.HideTip();
        this._edit = null;
        this._complete = null;
        A_LayerManager.HideFromRight(this._screen);
    }

    public fun TryHide(AUICodeEdit edit)
    {
        if (this._edit != edit) return;

        // 检查光标
        if (edit.cursor.line < this._complete.line_start || edit.cursor.line > this._line_end)
        {
            this.Hide();
            return;
        }

        if (edit.cursor.line == this._complete.line_start && edit.cursor.char < this._complete.char_start)
        {
            this.Hide();
            return;
        }

        if (edit.cursor.line == this._line_end && edit.cursor.char > this._char_end)
        {
            this.Hide();
            return;
        }
    }

    private fun HandleHideEvent(ALittle.UIHideEvent event)
    {
        this.HideTip();
        this._edit = null;
        this._complete = null;
    }

	// 显示TIP
	private fun ShowTip(string content)
	{
		// 创建模态提示对话框，并添加到模态层
		if (this._tip_dialog == null)
		{
			this._tip_dialog = g_Control.CreateControl{ALittle.DisplayObject}("ide_tool_area_tip", this);
            this._tip_dialog.width = 200;
		}

        A_LayerManager.AddToTip(this._tip_dialog);
		this._tip_dialog.visible = true;
		this._tip_text.text = content;
		this._tip_dialog.height = this._tip_text.real_height + 6;

		this._tip_dialog.x = this._screen.x;
		this._tip_dialog.y = this._screen.y + this._screen.height;
	}

	// 隐藏TIP
	private fun HideTip()
	{
		if (this._tip_dialog == null) return;
		this._tip_dialog.visible = false;
        A_LayerManager.RemoveFromTip(this._tip_dialog);
	}
}

protected var g_AUICodeCompleteScreen = new AUICodeCompleteScreen();
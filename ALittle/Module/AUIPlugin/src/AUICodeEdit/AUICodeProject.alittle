
namespace AUIPlugin;

protected var g_ABnfProjectMap:Map<string, AUICodeProject> = ALittle.CreateValueWeakMap();

public class AUICodeProject
{
    private int _query_id = 0;
    private lua.ABnfProject _project;

    private ALittle.LoopFrame _loop;
    private Map<int, ALittle.Thread> _map;

    public ctor(lua.ABnfProject project)
    {
        this._project = project;
        this._map = new Map<int, ALittle.Thread>();
    }

    public static CreateALittleScriptProject(string project_path) : AUICodeProject
    {
        var abnf_buffer = ALittle.File_ReadTextFromFile(g_ModuleBasePath.."Other/ABnf/ALittleScript.abnf");
        var project = new AUICodeProject(lua.alittlescript.create_alittlescript_project(project_path, abnf_buffer));
        project.Start();
        return project;
    }

    // 构建一个ABnf项目
    public static CreateABnfProject() : AUICodeProject
    {
        var abnf_project = g_ABnfProjectMap["abnf"];
        if (abnf_project == null)
        {
            var buffer = ALittle.File_ReadTextFromFile(g_ModuleBasePath.."/Other/ABnf/ABnf.abnf");
            if (buffer == null) buffer = "";

            var native_project = lua.abnf.create_abnf_project("", buffer);
            abnf_project = new AUICodeProject(native_project);
            g_ABnfProjectMap["abnf"] = abnf_project;
            abnf_project.Start();
        }
        return abnf_project;
    }

    public static Shutdown()
    {
        for (var name, project in g_ABnfProjectMap)
            project.Stop();
        g_ABnfProjectMap = ALittle.CreateValueWeakMap();
    }

    public get project() : lua.ABnfProject { return this._project; }
    public fun UpdateFile(string full_path) { lua.alanguage.abnfproject_updatefile(this._project, full_path); }
    public fun RemoveFile(string full_path) { lua.alanguage.abnfproject_removefile(this._project, full_path); }
    
    public fun Start()
    {
        if (this._loop != null) return;

        this._loop = new ALittle.LoopFrame(bind(this.Update, this));
        A_WeakLoopSystem.AddUpdater(this._loop);
    }

    public fun Add(ALittle.Thread thread) : int
    {
        ++ this._query_id;
        this._map[this._query_id] = thread;
        return this._query_id;
    }

    private fun Update(int frame)
    {
        while (true)
        {
            var info = lua.alanguage.abnfproject_pollone(this._project);
            if (info == null) break;

            var thread = this._map[info.query_id];
            if (thread != null)
            {
                this._map[info.query_id] = null;
                ALittle.Coroutine.Resume(thread, info.result);      
            }
        }
    }

    public fun Stop()
    {
        if (this._loop == null) return;

        A_WeakLoopSystem.RemoveUpdater(this._loop);
        this._loop = null;
    }
}
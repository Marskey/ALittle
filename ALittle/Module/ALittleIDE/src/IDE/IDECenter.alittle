
namespace ALittleIDE;

class IDECenter
{
	private ALittle.DisplayLayout _edit_container;		// 编辑器容器
    
	// 账号登录相关
	private IDEUIAccount _account;
	private IDEUIProjectDropdown _project_dropdown;
	private IDEUIMainMenu _main_menu;
	private IDEUICenter _center;
	
	public ctor() { }

	public await fun Setup()
	{
		// 创建主界面
		g_Control.CreateControl{ALittle.DisplayObject}("ide_main_scene", this, g_MainLayer);
		// 打开上一次的项目
		g_IDEProject.OpenLastProject();
		// 初始化组合键
		A_UISystem.keydown_callback = bind(this.HandleShortcutKey, this);
	}
	
	public fun Shutdown()
	{
		if (this._account != null) this._account.Shutdown();
		if (this._main_menu != null) this._main_menu.Shutdown();
	}

	public get center() : IDEUICenter { return this._center; }

	// 项目刷新
	public fun RefreshProject()
	{
		// 检查当前是否有打开的项目
		if (g_IDEProject.project == null) return;
		// 获取项目名
		var project_name = g_IDEProject.project.name;
		// 关闭当前项目
		g_IDEProject.CloseProject();
		// 打开当前项目
		g_IDEProject.OpenProject(project_name);
	}
	
	////////////////////////////////////////////////////////////////////////////////////////
	// 处理快捷点
	public fun HandleShortcutKey(int mod, int sym, int scancode)
	{
		// 判断F5，运行当前项目快捷键
		if (A_UISystem.sym_map[ALittle.UIEnumTypes.F5])
		{
			g_IDEProject.RunProject();
			return;
		}

		if (this._center != null)
			this._center.HandleShortcutKey();
	}
}

protected var g_IDECenter = new IDECenter();
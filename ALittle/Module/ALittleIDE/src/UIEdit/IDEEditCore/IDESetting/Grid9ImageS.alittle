
namespace ALittleIDE;

public class Grid9ImageS : Grid9S
{
    private ALittle.DisplayObject _texture_name;

    public ctor(IDETreeUserInfo user_info, IDETabChild tab_child, IDETreeLogic tree_logic)
    {
        // 设置面板的UI
        this._layer_name = "ide_setting_grid9image";
    }
    
    public fun LoadNatureBase()
    {
        Grid9S.LoadNatureBase(this);
        this.LoadDefaultNilString("texture_name");
        this.LoadEnumData("flip", cast<Map<string, any>>(g_IDEEnum.flip_type));
    }
    
    public fun HandleImageTextureNameFOCUSOUT(ALittle.UIEvent event)
    {
        this.DefaultNilStringInputChange("texture_name", false);
        this.HandleAutoCut(event);
    }
    public fun HandleImageTextureNameSelect(ALittle.UIEvent event)
    {
        var func = bind(this.ImagePathSelectCallback, this, "texture_name", this.HandleImageTextureNameFOCUSOUT, null);
        g_IDEAttrImageDialog.ShowDialog(null, func);
    }
    public fun HandleFlipSELECT_CHANGE(ALittle.UIEvent event)
    {
        var list = g_IDEEnum.flip_rtype;
        this.TypeSelectChange("flip", cast<Map<string, any>>(list), false);
    }
    public fun HandleAutoCut(ALittle.UIEvent event)
    {
        var image_path = this._texture_name.text;
        var display_info = IDEUtility_GenerateGrid9ImageInfo(g_IDEProject.project.base_path.."Texture/", image_path);
        if (display_info == null) return;
        var revoke_bind = new IDERevokeBind();
        this._left_size.text = display_info.left_size;
        this.ValueNumInputChange("left_size", false, revoke_bind);
        this._right_size.text = display_info.right_size;
        this.ValueNumInputChange("right_size", false, revoke_bind);
        this._top_size.text = display_info.top_size;
        this.ValueNumInputChange("top_size", false, revoke_bind);
        this._bottom_size.text = display_info.bottom_size;
        this.ValueNumInputChange("bottom_size", false, revoke_bind);
        this._tab_child.revoke_list.PushRevoke(revoke_bind);
    }
}

namespace ALittle;

public class SpringRadioButton : SpringCheckButton
{
    private string  _group_name;
    private bool    _cancel_select;
    
    public ctor(ControlSystem ctrl_sys)
    {
        // this._group_name = null;
        this._cancel_select = false;   // 是否允许取消选择
    }
    
    public set group_name(string value)
    {
        A_TextRadioButtonManager.SetGroupName(this, this._group_name, value);
        this._group_name = value;
    }
    
    public get group_name() : string
    {
        return this._group_name;
    }
    
    public set cancel_select(bool value)
    {
        this._cancel_select = value;
    }
    
    public get cancel_select() : bool
    {
        return this._cancel_select;
    }

    protected fun HandleLButtonUp(UILButtonUpEvent event)
    {
        if (event.rel_x >= 0 && event.rel_y >= 0 && event.rel_x < event.target._width && event.rel_y < event.target._height)
        {
            if (this._selected == false)
            {
                this._selected = true;
                // 获取同组的单选按钮
                var group = A_TextRadioButtonManager.GetGroupByName(this._group_name);
                if (group != null)
                {
                    for (var k:SpringRadioButton, _ in group)
                    {
                        // 设置为false
                        if (k != this && k._selected == true)
                        {
                            k._selected = false;
                            k.ShowUp();
                            k.DispatchEvent(new UIChangedEvent());
                        }
                    }
                }
                this.DispatchEvent(new UIChangedEvent());
            }
            elseif (this._cancel_select)
            {
                this._selected = false;
                this.DispatchEvent(new UIChangedEvent());
            }
            var e = new UIClickEvent();
            e.is_drag = event.is_drag;
            this.DispatchEvent(e);

            if (System_IsPhone == false)
                this.ShowOver();
            else
                this.ShowUp();
        }
        else
        {
            this.ShowUp();
        }
    }
    
    public set selected(bool value)
    {
        if (this._selected == value)
            return;
        this._selected = value;
    
        if (this._abs_disabled)
            this.ShowDisabled();
        else
            this.ShowUp();
    
        if (this._selected == false)
            return;

        // 获取同组的单选按钮
        var group = A_TextRadioButtonManager.GetGroupByName(this._group_name);
        if (group != null)
        {
            for (var k:SpringRadioButton, _ in group)
            {
                // 设置为false
                if (k != this && k._selected == true)
                {
                    k._selected = false;
                    k.ShowUp();
                }
            }
        }
    }
}
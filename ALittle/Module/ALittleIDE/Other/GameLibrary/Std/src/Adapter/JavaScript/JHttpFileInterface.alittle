
[Language(JavaScript)]
namespace JavaScript;

private var __JHTTPFILE_MAXID:int = 0;

public class JHttpFileInterface : ALittle.IHttpFileSenderNative
{
    private int _id;
    private string _url;
    private string _file_path;
    private bool _download;
    private javascript.XMLHttpRequest _request;
    private Functor<(ALittle.IHttpFileSenderNative, string)> _failed_callback;
    private Functor<(ALittle.IHttpFileSenderNative)> _succeed_callback;
    private Functor<(ALittle.IHttpFileSenderNative)> _process_callback;
    
    private int _cur_size;
    private int _total_size;
    
    public ctor()
    {
        this._cur_size = 0;
        this._total_size = 0;
        ++ __JHTTPFILE_MAXID;
        this._id = __JHTTPFILE_MAXID;
    }
    public fun GetID() : int { return this._id; }
    
    // start_size暂时不支持
    public fun SetURL(string url, string file_path, bool download, int start_size)
    {
        this._url = url;
        this._file_path = file_path;
        this._download = download;
    }
    public fun Start(Functor<(ALittle.IHttpFileSenderNative, string)> failed_callback
                    , Functor<(ALittle.IHttpFileSenderNative)> succeed_callback
                    , Functor<(ALittle.IHttpFileSenderNative)> process_callback)
    {
        var content:string;
        if (!this._download)
        {
            content = File_LoadFile(this._file_path);
            if (content == null)
            {
                if (failed_callback != null)
                    failed_callback(this, "file is not exist:" .. this._file_path);
                return;
            }
        }
        
        this._failed_callback = failed_callback;
        this._succeed_callback = succeed_callback;
        this._process_callback = process_callback;
            
        this._request = new javascript.XMLHttpRequest();
        if (this._download)
            this._request.open("GET", this._url, true);
        else
            this._request.open("POST", this._url, true);
        
		var error_func = bind(this.HandleAjaxError, this);
        this._request.onerror = error_func;
		this._request.ontimeout = error_func;
        this._request.onload = bind(this.HandleStartCompleted, this);
        this._request.onprogress = bind(this.HandleOnProgress, this);
        
        if (this._download)
            this._request.send(null);
        else
            this._request.send(this._file_path);
    }
    public fun Stop()
    {
        if (this._request != null)
            this._request.abort();
    }
    
    public fun GetPath() : string { return this._file_path; }
    public fun GetCurrentSize() : int { return this._cur_size; }
    public fun GetTotalSize() : int { return this._total_size; }
    
    public fun GetContent() : string { return this._request.responseText; }
    
    private fun HandleAjaxError()
	{
        if (this._failed_callback != null)
            this._failed_callback(this, this._request.statusText);
	}
    
	private fun HandleStartCompleted()
	{
        if (this._download && !File_SaveFile(this._file_path, this._request.responseText))
        {
            if (this._failed_callback != null)
                this._failed_callback(this, "file save failed:" .. this._file_path);
            return;
        }
        
        if (this._succeed_callback != null)
            this._succeed_callback(this);
	}
    
    private fun HandleOnProgress(javascript.XMLHttpRequestOnProgressEvent event)
    {
        this._cur_size = event.loaded;
        this._total_size = event.total;
        
        if (this._process_callback != null)
            this._process_callback(this);
    }
}
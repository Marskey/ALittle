
namespace ALittle;

// 结构体反射信息
public struct StructInfo
{
    string name;                 // 带命名域的结构体名
    string ns_name;              // 所在命名域名
    string rl_name;              // 实际名
    int hash_code;               // 对name进行计算的哈希值
    List<string> name_list;      // 变量名列表
    List<string> type_list;      // 变量类型列表
    Map<string, string> option_map; // 附加信息映射表
}

// 类反射信息
public struct ClassInfo
{
    string __name;                      // 类名
    ClassInfo __super;                  // 父类信息
    List<ClassInfo> __element;          // 模板元素
    Map<string, ClassInfo> __child;     // 继承于当前的子模板
    Map<string, Functor<(any):any>> __getter;    // getter
    Map<string, Functor<(any, any)>> __setter;   // setter
}

// 递归调用构造函数
[Language(JavaScript)]
protected static ClassCtor(any object, ALittle.ClassInfo clazz, ...)
{
    var drived = clazz.__super;
    if (drived != null) ClassCtor(object, drived, ...);
    
    var fctor = cast<Map<string, javascript.Function>>(clazz)["Ctor"];
    if (fctor != null) fctor.call(object, ...);
}

// 根据反射信息创建对象
public static NewObject(ClassInfo clazz, ...) : any
{
    [Language(LuaJIT)]
    return cast<Functor<(any):any>>(clazz)(...);
    
    // 构造对象
    [Language(JavaScript)]
    var object = javascript.Object.create(clazz);
	// 把自己作为类保存在对象中
    [Language(JavaScript)]
    cast<Map<string, any>>(object)["__class"] = clazz;
    // 调用构造函数
    [Language(JavaScript)]
    ClassCtor(object, clazz, ...);
    // 返回对象
    [Language(JavaScript)]
    return object;
}